dnl The configure script is generated by autogen.sh from configure.base 
dnl and the various configure.add files in the source tree.  Edit 
dnl configure.base and reprocess rather than modifying ./configure.

dnl autoconf 2.13 certainly doesn't work! What is the minimum requirement?
AC_PREREQ(2.2)

AC_INIT(configure.base)

MKOCTFILE=mkoctfile
AC_SUBST(MKOCTFILE)

dnl Strip on windows, don't strip on Mac OS/X or IRIX
dnl For the rest, you can force strip using MKOCTFILE="mkoctfile -s"
dnl or avoid strip using STRIP=: before ./configure
case "$canonical_host_type" in
    powerpc-apple-darwin*|*-sgi-*)
	STRIP=:
    ;;
    *-cygwin-*|*-mingw-*) 
	MKOCTFILE="$MKOCTFILE -s" 
    ;;
esac

dnl Define the following macro:
dnl    OF_CHECK_LIB(lib,fn,true,false,helpers)
dnl This is just like AC_CHECK_LIB, but it doesn't update LIBS
AC_DEFUN([OF_CHECK_LIB],
[save_LIBS="$LIBS"
AC_CHECK_LIB($1,$2,$3,$4,$5)
LIBS="$save_LIBS"
])

AC_DEFINE(have_jpeg)
AC_CHECK_HEADER(jpeglib.h, have_jpeg=yes, have_jpeg=no)
if test $have_jpeg = yes ; then
    OF_CHECK_LIB(jpeg, jpeg_std_error, have_jpeg=yes, have_jpeg=no)
    if test $have_jpeg = no ; then
	IMAGESTATUS="libjpeg not found"
    else
	IMAGESTATUS="jpeg"
	AC_SUBST(DEFHAVE_JPEG)
	DEFHAVE_JPEG="HAVE_JPEG=1"
    fi
else
    IMAGESTATUS="jpeglib.h not found"
fi

AC_DEFINE(have_png)
AC_CHECK_HEADER(png.h, have_png=yes, have_png=no)
if test $have_png = yes ; then
    OF_CHECK_LIB(png, png_set_sig_bytes, have_png=yes, have_png=no)
    if test $have_png = no ; then
	IMAGESTATUS="$IMAGESTATUS, libpng not found"
    else
	IMAGESTATUS="$IMAGESTATUS, png"
	AC_SUBST(DEFHAVE_PNG)
	DEFHAVE_PNG="HAVE_PNG=1"
    fi
else
    IMAGESTATUS="$IMAGESTATUS, png.h not found"
fi

AC_CHECK_PROG(HAVE_MAGICKXX, Magick++-config, yes)
if test $HAVE_MAGICKXX ; then
	IMAGESTATUS="$IMAGESTATUS, ImageMagick++"
	AC_SUBST(DEFHAVE_MAGICKXX)		
	DEFHAVE_MAGICKXX="HAVE_MAGICKXX=1"	
else
    IMAGESTATUS="$IMAGESTATUS, ImageMagick++ not found"
fi

CONFIGURE_OUTPUTS="Makeconf"
